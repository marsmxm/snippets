#lang pie

(claim +
  (-> Nat Nat
      Nat))
(define +
  (λ (m n)
    (iter-Nat m
      n
      (λ (+-m-1)
        (add1 +-m-1)))))

(claim double
  (-> Nat Nat))
(define double
  (λ (n)
    (iter-Nat n
      0
      (+ 2))))


(claim Even
  (-> Nat U))
(define Even
  (λ (n)
    (Σ ((half Nat))
      (= Nat n (double half)))))

(claim zero-is-even
  (Even 0))
(define zero-is-even
  (cons 0 (same 0)))

(claim ten-is-even
  (Even 10))
(define ten-is-even
  (cons 5 (same 10)))


(claim +two-even
  (Π ((n Nat))
    (-> (Even n)
        (Even (+ 2 n)))))
(define +two-even
  (λ (n)
    (λ (even-n)
      (cons (add1 (car even-n))
        (cong (cdr even-n) (+ 2))))))


(claim two-is-even
  (Even 2))
(define two-is-even
  (+two-even 0 zero-is-even))


(claim Odd
  (-> Nat U))
(define Odd
  (λ (n)
    (Σ ((haf Nat))
      (= Nat n (add1 (double haf))))))

(claim one-is-odd
  (Odd 1))
(define one-is-odd
  (cons 0 (same 1)))

(claim thirteen-is-odd
  (Odd 13))
(define thirteen-is-odd
  (cons 6 (same 13)))

(claim add1-even->odd
  (Π ((n Nat))
    (-> (Even n)
        (Odd (add1 n)))))
(define add1-even->odd
  (λ (n)
    (λ (e-n)
      (cons (car e-n)
        (cong (cdr e-n) (+ 1))))))


(claim add1-odd->even
  (Π ((n Nat))
    (-> (Odd n)
        (Even (add1 n)))))
(define add1-odd->even
  (λ (n)
    (λ (o-n)
      (cons (+ 1 (car o-n))
        (cong (cdr o-n) (+ 1))))))


(claim repeat
  (-> (-> Nat
          Nat)
      Nat
      Nat))
(define repeat
  (λ (f n)
    (iter-Nat n
      (f 1)
      (λ (iter-f-n-1)
        (f iter-f-n-1)))))

(claim ackermann
  (-> Nat Nat
      Nat))
(define ackermann
  (λ (n)
    (iter-Nat n
      (+ 1)
      (λ (ackermann-n-1)
        (repeat ackermann-n-1)))))
